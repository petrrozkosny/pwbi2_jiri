dim_datumy = 
ADDCOLUMNS(
SELECTCOLUMNS(
    CALENDARAUTO(),"datum",[Date]),
"rok",YEAR([datum]),
"kvartal",QUARTER([datum]),
"mesic",MONTH([datum]),
"cislo_tydne",WEEKNUM([datum],21))

____________________________________________________________

ceske_lokality = 
-- SELECT dim_lokality[Lokalita] FROM dim_lokality
-- WHERE dim_lokality[Zeme] = "Cesko"
UNION(
FILTER(dim_lokality,dim_lokality[Zeme] = "Cesko"),
ROW("Lokalita","Ostatni","Mesto","Ostatni","Zeme","Ostatni"))
__________________________________________________________
01 suma srazek = SUM(data[Srazky])
______________________________________________
02 srazky ruzyne = 

SUMX(
    FILTER(
    ALL(data),
    data[Lokalita] = "RUZYNE"),
data[Srazky])
_____________________________________________________
03 srazky ruzyne calculate1 = 
-- takhle ne
CALCULATE(
    SUM(data[Srazky]),
FILTER(
    ALL(data),
data[Lokalita] = "RUZYNE"))

_______________________________________________

04 srazky ruzyne calculate2 = 
// -- takhle ano, pokud nefunguje calculate3
CALCULATE(
    SUM(data[Srazky]),
FILTER(
    ALL(data[Lokalita]),
data[Lokalita] = "RUZYNE"))

______________________________________________

05 srazky ruzyne calculate3 = 
// -- takhle ano
CALCULATE(
SUM(data[Srazky]),
data[Lokalita] = "RUZYNE")

_________________________________________________

06 suma srazek vse = 

CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy[rok]),
    ALL(data[Lokalita]))

_____________________________________________________

07 suma srazek cesko = 
-- vsechny roky

CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy[rok]),
    all(data[Lokalita]),
    dim_lokality[Zeme] = "Cesko")
    
 ________________________________________________________
 
 09 suma srazek predchozi rok = 
-- tj. suma srazek v dane lokalite, v roce o jedno mensi
CALCULATE(
    -- agregacni funkce
    SUM(data[Srazky]),
    FILTER(
        ALL(dim_datumy[rok]),
        dim_datumy[rok] = SELECTEDVALUE(dim_datumy[rok])-1)
        )
__________________________________________________


10 pocet dnu prselo = 
-- pocet dnu v danem roce, lokalite, kdy srazky >0
CALCULATE(
    COUNT(data[Datum]),
data[Srazky] >0)

_______________________________

11 mesic max. teploty = 

CALCULATE(
    MONTH(
    MIN(data[Datum])),
    data[Teplota] = MAX(data[Teplota]))
 ______________________________________________
 
 12 suma srazek kumulativne v lokalite = 

-- suma srazek v danem a predchozich letech

CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[rok] <= SELECTEDVALUE(dim_datumy[rok]))
______________________________________________

13 poprve prselo = 
-- v danem roce, lokalite
CALCULATE(
    -- minimalni datum
    MIN(data[Datum]),
    data[Srazky]>0)

________________________________________________

14 srazky kumulativne = 
CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[mesic] <= SELECTEDVALUE(dim_datumy[mesic]))
    
  _____________________________________________
  15 suma srazek kumulativne ruzyne = 
CALCULATE(
    SUM(data[Srazky]),
    dim_lokality[Lokalita] = "RUZYNE",
    dim_datumy[mesic] <=SELECTEDVALUE(dim_datumy[mesic]))
    
 ___________________________________________________
 
 16 suma srazek predchozi rok = 
CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[rok] = SELECTEDVALUE(dim_datumy[rok]) - 1)
  
_______________________________________

17 kontrola ruzyne = 

VAR max_teplota = MAX(data[Teplota])
VAR max_teplota_ruzyne = CALCULATE(MAX(data[Teplota]),dim_lokality[Lokalita]= "RUZYNE")

RETURN
    IF(max_teplota_ruzyne = max_teplota,"max teplota", "nebyla max teplota")

________________________________________

18 pocet destivych dnu ruzyne kumulativne = 
CALCULATE(
    COUNT(data[Datum]),
    dim_lokality[Lokalita] = "RUZYNE",
    data[Srazky] >0,
    dim_datumy[mesic] <= SELECTEDVALUE(dim_datumy[mesic]))
    
  ______________
  
  19  minimalni datum max teploty ruzyne = 

-- max teplota ruzyni
VAR max_teplota_ruzyne = 
CALCULATE(
    MAX(data[Teplota]),
    dim_lokality[Lokalita] = "RUZYNE")

return
CALCULATE(
    MIN(data[Datum]),
    dim_lokality[Lokalita] = "RUZYNE",
    data[Teplota] = max_teplota_ruzyne)
    
____________________________
20 lokality se srazkami nad 100 = 

    COUNTX(
    FILTER(
    ADDCOLUMNS(
    VALUES(data[Lokalita]),
    "suma srazek",CALCULATE(SUM(data[Srazky]))),
    [suma srazek] >100),data[Lokalita])
    
 ____________________________________________
 
 21 maximalni mesicni srazky = 
-- suma srazek v danem mesici a roce
VAR srazky = SUM(data[Srazky])
-- suma srazek kazdeho jednoho mesice
-- z tohoto vypocitat maximum
VAR maximalni_mesicni_srazky  =
MAXX(
    ALL(dim_datumy[mesic]),
    CALCULATE(SUM(data[Srazky])))
RETURN
IF(srazky = maximalni_mesicni_srazky,"CYAN")


_____________________________________________

22 rank srazek = 

VAR tabulka = 

    ADDCOLUMNS(
    ALL(dim_datumy[mesic]),
    "mesicni_srazky",
    CALCULATE(SUM(data[Srazky])))

VAR tabulka_rank = ADDCOLUMNS(tabulka,"rank srazek",RANKX(tabulka,[mesicni_srazky]))

return 
MINX(FILTER(tabulka_rank,dim_datumy[mesic] = SELECTEDVALUE(dim_datumy[mesic])),[rank srazek])
_______________________________________________

23 rank srazek barva = 

VAR tabulka = 
    ADDCOLUMNS(
    ALL(dim_datumy[mesic]),
    "mesicni_srazky",
    CALCULATE(SUM(data[Srazky])))

VAR tabulka_rank = ADDCOLUMNS(tabulka,"rank srazek",RANKX(tabulka,[mesicni_srazky]))
return 
IF(
MINX(FILTER(tabulka_rank,dim_datumy[mesic] = SELECTEDVALUE(dim_datumy[mesic])),[rank srazek])<=3,"CYAN")

_______________________________________________

24 lokality s rocnimi srazkami = 
if(ISFILTERED(dim_datumy[rok]),
CONCATENATEX(
filter(
    ADDCOLUMNS(
        VALUES(dim_lokality[Lokalita]),
    "rocni srazky",
    CALCULATE(SUM(data[Srazky]))
    )
,[rocni srazky] > 1000)
,dim_lokality[Lokalita],", "
,BLANK()))

__________________________________________________________________

25 suma srazek kumulativne zeme = 


CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[mesic] <= SELECTEDVALUE(dim_datumy[mesic]),
    ALL(dim_lokality[Lokalita]),
    dim_lokality[Zeme] IN VALUES(dim_lokality[Zeme]))
    
    
_____________________________________________________________________


26 nejvyssi mesicni srazky = 

VAR srazky = SUM(data[Srazky])
VAR maximalni_mesicni_srazky = 
    MAXX(
    ALL(dim_datumy[mesic]),
    CALCULATE(SUM(data[Srazky])))

RETURN  
IF(srazky = maximalni_mesicni_srazky,"CYAN")
